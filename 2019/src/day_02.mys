from os.path import Path

func get_input(filename: string) -> [i64]:
    return [
        i64(value)
        for value in Path(f"{__assets__}/day_02/{filename}").read_text().strip().split(",")
    ]

func run(program: [i64]):
    for i in range(0, program.length(), 4):
        match program[i]:
            case 1:
                in1 = program[i + 1]
                in2 = program[i + 2]
                out = program[i + 3]
                program[out] = program[in1] + program[in2]
            case 2:
                in1 = program[i + 1]
                in2 = program[i + 2]
                out = program[i + 3]
                program[out] = program[in1] * program[in2]
            case 99:
                break

func part_1(filename: string) -> i64:
    program = get_input(filename)
    program[1] = 12
    program[2] = 2
    run(program)

    return program[0]

func part_2(filename: string) -> i64:
    for noun in range(100):
        for verb in range(100):
            program = get_input(filename)
            program[1] = noun
            program[2] = verb
            run(program)

            if program[0] == 19690720:
                return 100 * noun + verb

    raise NotImplementedError("no noun and verb found")

func day_02():
    print(part_1("input.txt"))
    print(part_2("input.txt"))

test all():
    assert part_1("input.txt") == 6087827
    assert part_2("input.txt") == 5379
