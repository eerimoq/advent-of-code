from os.path import Path

@generic(T)
func union(items: [{T}]) -> {T}:
    result = {}

    for item in items:
        result |= item

    return result

@generic(T)
func intersection(items: [{T}]) -> {T}:
    result = {}

    if items.length() > 0:
        result |= items[0]

        for item in slice(items, 1):
            result &= item

    return result

func load_answers() -> [[{char}]]:
    answers = []

    for group in Path(f"{__assets__}/day_06.txt").read_text().strip().split("\n\n"):
        group_answers: [{char}] = []

        for person_answers_string in group.split("\n"):
            person_answers: {char} = {}

            for answer in person_answers_string:
                person_answers.add(answer)

            group_answers.append(person_answers)

        answers.append(group_answers)

    return answers

func part_1(answers: [[{char}]]):
    count: i64 = 0

    for group_answers in answers:
        count += union[char](group_answers).length()

    print(count)

func part_2(answers: [[{char}]]):
    count: i64 = 0

    for group_answers in answers:
        count += intersection[char](group_answers).length()

    print(count)

func day_06():
    answers = load_answers()
    part_1(answers)
    part_2(answers)
