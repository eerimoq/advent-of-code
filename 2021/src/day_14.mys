from os.path import Path

func get_input(filename: string) -> ([char], {char: {char: char}}):
    lines = Path(f"{__assets__}/day_14/{filename}").read_text().strip().split("\n")
    polymer = list(lines[0])
    insert: {char: {char: char}} = {}

    for line in slice(lines, 2):
        if line[0] not in insert:
            insert[line[0]] = {}

        insert[line[0]][line[1]] = line[6]

    return (polymer, insert)

func increment_pair(pairs: {char: {char: i64}},
                    left: char,
                    right: char,
                    count: i64):
    if left not in pairs:
        pairs[left] = {}

    pairs_inner = pairs[left]

    if right not in pairs_inner:
        pairs_inner[right] = 0

    pairs_inner[right] += count

func increment_count(counts: {char: i64}, ch: char, count: i64):
    if ch not in counts:
        counts[ch] = 0

    counts[ch] += count

func both_parts(filename: string, steps: i64) -> i64:
    polymer, insert = get_input(filename)
    pairs: {char: {char: i64}} = {}
    counts: {char: i64} = {}

    for i in range(polymer.length() - 1):
        increment_pair(pairs, polymer[i], polymer[i + 1], 1)

    for ch in polymer:
        increment_count(counts, ch, 1)

    for _ in range(steps):
        next_pairs: {char: {char: i64}} = {}

        for left, value in pairs:
            for right, count in value:
                ch = insert[left][right]
                increment_count(counts, ch, count)
                increment_pair(next_pairs, left, ch, count)
                increment_pair(next_pairs, ch, right, count)

        pairs = next_pairs

    return max(counts.values()) - min(counts.values())

func part_1(filename: string) -> i64:
    return both_parts(filename, 10)

func part_2(filename: string) -> i64:
    return both_parts(filename, 40)

func day_14():
    print(part_1("input.txt"))
    print(part_2("input.txt"))

test all():
    assert part_1("example.txt") == 1588
    assert part_2("example.txt") == 2188189693529
    assert part_1("input.txt") == 3095
    assert part_2("input.txt") == 3152788426516
